using ExaPowerIO, Test, PowerModels, PGLib, Logging

# PowerModels emits its warnings through a private `ConsoleLogger` it installs on
# itself, and wraps every call site in `with_logger`, so the global logger never
# sees them. Pointing that logger at a buffer is the only way to read them back.
const PM_LOG = IOBuffer()
PowerModels._LOGGER[] = Logging.ConsoleLogger(PM_LOG, Logging.Info)

@views function pglib_num_buses(s::String)
    s = s[length("pglib_opf_case")+1:end]
    s = s[1:findfirst(c -> !isdigit(c), s)-1]
    return parse(Int, s)
end
const PGLIB_CASES = sort!(PGLib.find_pglib_case(""); by=pglib_num_buses)
const FILE_CASES = ["../data/$case" for case in readdir("../data") if case != "LICENSE"]

function fields_excluding(::Type{T}, exclude::Vector{Symbol}) where T
    filter(f -> !(f in exclude), fieldnames(T))
end

# this is copied from the old parser.jl
# power models filters out inactive branches
function parse_pm(filename, num_branch)
    data = PowerModels.parse_file(filename)
    PowerModels.standardize_cost_terms!(data, order = 2)
    PowerModels.calc_thermal_limits!(data)

    ref = PowerModels.build_ref(data)[:it][:pm][:nw][0]

    arc = Dict()
    for (i, b) in ref[:branch]
        merge!(arc, Dict(i => (;:bus => b["f_bus"], :rate_a => b["rate_a"],),
                                 (i+num_branch) => (;:bus => b["t_bus"], :rate_a => b["rate_a"],)))
    end

    data =  (
        version = ref[:source_version],
        baseMVA = ref[:baseMVA],
        bus = Dict(
            begin
                bus_loads = [ref[:load][l] for l in ref[:bus_loads][k]]
                bus_shunts = [ref[:shunt][s] for s in ref[:bus_shunts][k]]
                k => (;
                 :pd => sum(load["pd"] for load in bus_loads; init = 0.0),
                 :gs => sum(shunt["gs"] for shunt in bus_shunts; init = 0.0),
                 :qd => sum(load["qd"] for load in bus_loads; init = 0.0),
                 :bs => sum(shunt["bs"] for shunt in bus_shunts; init = 0.0),
                 :baseKV => v["base_kv"],
                 :type => v["bus_type"],
                 (Symbol(s) => v[s] for s in ["bus_i", "area", "vm", "va", "zone", "vmax", "vmin"])...,
                )
            end for (k, v) in ref[:bus]
        ),
        gen = Dict(
            k => (;
                :c => ntuple(i -> v["cost"][i], 3),
                :n => v["ncost"],
                :bus => v["gen_bus"],
                :model_poly => v["model"] == 2,
                :status => v["gen_status"],
                (Symbol(s) => v[s] for s in ["pg", "qg", "qmax", "qmin", "vg", "mbase", "pmax", "pmin", "startup", "shutdown"])...,
            ) for (k, v) in ref[:gen]
        ),
        arc = arc,
        branch = Dict(
            begin
                g, b = PowerModels.calc_branch_y(branch)
                tr, ti = PowerModels.calc_branch_t(branch)
                ttm = tr^2 + ti^2
                g_fr = branch["g_fr"]
                b_fr = branch["b_fr"]
                g_to = branch["g_to"]
                b_to = branch["b_to"]
                c1 = (-g * tr - b * ti) / ttm
                c2 = (-b * tr + g * ti) / ttm
                c3 = (-g * tr + b * ti) / ttm
                c4 = (-b * tr - g * ti) / ttm
                c5 = (g + g_fr) / ttm
                c6 = (b + b_fr) / ttm
                c7 = (g + g_to)
                c8 = (b + b_to)
                i => (;
                    :j => 1,
                    :f_idx => i,
                    :t_idx => i + num_branch,
                    :f_bus => branch["f_bus"],
                    :t_bus => branch["t_bus"],
                    :c1 => c1,
                    :c2 => c2,
                    :c3 => c3,
                    :c4 => c4,
                    :c5 => c5,
                    :c6 => c6,
                    :c7 => c7,
                    :c8 => c8,
                    :status => branch["br_status"],
                    (Symbol(s) => branch[s] for s in ["br_r", "br_x","b_fr", "b_to", "g_fr", "g_to", "rate_a", "rate_b", "rate_c", "tap", "shift", "angmin", "angmax"])...,
                )
            end for (i, branch) in ref[:branch]
        ),
        storage = isempty(ref[:storage]) ?  empty_data = Dict{Int, NamedTuple{(:i,), Tuple{Int64}}}() : Dict(
            begin
                i => (;:c => i,
                :Zr => stor["r"],
                :Zim => stor["x"],
                :Pexts => stor["ps"],
                :Qexts => stor["qs"],
                (Symbol(s) => stor[s] for s in ["storage_bus", "energy", "energy_rating", "charge_rating", "discharge_rating", "discharge_efficiency", "thermal_rating", "charge_efficiency", "qmin", "qmax", "p_loss", "q_loss", "status"])...,
               )
            end for (i, stor) in ref[:storage]
        ),
    )

    return data
end

function compare_fields(lhs::L, rhs::R, fields) where {L,R}
    for field in fields
        if !isapprox(getfield(lhs, field), getfield(rhs, field))
            @info field
            @info lhs
            @info rhs
        end
        @test isapprox(getfield(lhs, field), getfield(rhs, field))
    end
end

function test_case(ep_filtered, ep_unfiltered, pm_output, pm_log, dataset)
    # when the reference bus gets changed, and there is a tie in pmax, the new ref is unknown
    if occursin("as reference based on generator", pm_log)
        @info "Skipping case $dataset due to changed reference bus"
        return
    end
    @test pm_output.version == ep_filtered.version
    @test isapprox(pm_output.baseMVA, ep_filtered.baseMVA)
    for (i, ep_bus) in enumerate(ep_filtered.bus)
        pm_bus = pm_output.bus[ep_bus.bus_i]
        compare_fields(ep_bus, pm_bus, fields_excluding(ExaPowerIO.BusData, [:i]))
    end

    filtered_ind = 1
    @test length(ep_filtered.gen) == length(pm_output.gen)
    for (i, ep_gen) in enumerate(ep_unfiltered.gen)
        ep_gen.status == 0 && continue
        ep_gen = ep_filtered.gen[filtered_ind]
        pm_gen = pm_output.gen[i]
        compare_fields(ep_gen, pm_gen, fields_excluding(ExaPowerIO.GenData, [:i, :bus]))
        @test ep_filtered.bus[ep_gen.bus].bus_i == pm_gen.bus
        delete!(pm_output.gen, i)
        filtered_ind += 1
    end
    @test isempty(pm_output.gen)

    filtered_ind = 1
    @test length(ep_filtered.branch) == length(pm_output.branch)
    for (i, ep_branch) in enumerate(ep_unfiltered.branch)
        ep_branch.status == 0 && continue
        ep_branch = ep_filtered.branch[filtered_ind]
        pm_branch = pm_output.branch[i]
        ep_tbus = ep_filtered.bus[ep_branch.t_bus].bus_i
        ep_fbus = ep_filtered.bus[ep_branch.f_bus].bus_i
        if pm_branch.f_bus == ep_tbus && pm_branch.t_bus == ep_fbus
            ep_branch = BranchData{Float64}(
                ep_branch.i,
                ep_branch.t_bus,
                ep_branch.f_bus,
                ep_branch.br_r * ep_branch.tap^2,
                ep_branch.br_x * ep_branch.tap^2,
                ep_branch.b_to / ep_branch.tap^2,
                ep_branch.b_fr * ep_branch.tap^2,
                ep_branch.g_to / ep_branch.tap^2,
                ep_branch.g_fr * ep_branch.tap^2,
                ep_branch.rate_a,
                ep_branch.rate_b,
                ep_branch.rate_c,
                1 / ep_branch.tap,
                -ep_branch.shift,
                ep_branch.status,
                -ep_branch.angmax,
                -ep_branch.angmin,
                # we arent using pm arc calculations so no need to flip
                ep_branch.f_idx,
                ep_branch.t_idx
            )
            ep_filtered.arc[i], ep_filtered.arc[i+length(ep_filtered.branch)] = ep_filtered.arc[i+length(ep_filtered.branch)], ep_filtered.arc[i]
            ep_tbus = ep_filtered.bus[ep_branch.t_bus].bus_i
            ep_fbus = ep_filtered.bus[ep_branch.f_bus].bus_i
        end
        @test ep_fbus == pm_branch.f_bus
        @test ep_tbus == pm_branch.t_bus
        @test ep_filtered.arc[ep_branch.f_idx].bus == ep_branch.f_bus
        @test ep_filtered.arc[ep_branch.t_idx].bus == ep_branch.t_bus
        compare_fields(ep_branch, pm_branch, fields_excluding(ExaPowerIO.BranchData, [:i, :f_bus, :t_bus, :f_idx, :t_idx]))
        delete!(pm_output.branch, i)
        filtered_ind += 1
    end
    @test isempty(pm_output.branch)

    filtered_ind = 1
    @test length(ep_filtered.arc) == length(pm_output.arc)
    for (i, ep_arc) in enumerate(ep_unfiltered.arc)
        if !haskey(pm_output.arc, i)
            continue
        end
        ep_arc = ep_filtered.arc[filtered_ind]
        pm_arc = pm_output.arc[i]
        compare_fields(ep_arc, pm_arc, [:rate_a])
        @test ep_filtered.bus[ep_arc.bus].bus_i == pm_arc.bus
        delete!(pm_output.arc, i)
        filtered_ind += 1
    end
    @test isempty(pm_output.arc)

    @test length(ep_filtered.storage) == length(pm_output.storage)
    for (i, ep_storage) in enumerate(ep_filtered.storage)
        pm_storage = pm_output.storage[i]
        compare_fields(ep_storage, pm_storage, fields_excluding(ExaPowerIO.StorageData, [:i]))
    end
end

@testset "ExaPowerIO parsing tests" begin
    PGLib_opf = ExaPowerIO.get_path(:pglib)

    for dataset in FILE_CASES
        take!(PM_LOG)
        @info "Testing with dataset: $dataset"
        ep_filtered = ExaPowerIO.parse_matpower(dataset)
        ep_unfiltered = ExaPowerIO.parse_matpower(dataset; filtered=false)
        pm_output = parse_pm(dataset, length(ep_unfiltered.branch))
        test_case(ep_filtered, ep_unfiltered, pm_output, String(take!(PM_LOG)), dataset)
    end
    for dataset in PGLIB_CASES
        take!(PM_LOG)
        @info "Testing with pglib dataset: $dataset"
        path = joinpath(PGLib_opf, dataset)
        @info path
        ep_filtered = ExaPowerIO.parse_matpower(dataset; library=:pglib)
        ep_unfiltered = ExaPowerIO.parse_matpower(dataset; library=:pglib, filtered=false)
        pm_output = parse_pm(path, length(ep_unfiltered.branch))
        test_case(ep_filtered, ep_unfiltered, pm_output, String(take!(PM_LOG)), dataset)
    end
end

ROW_TYPES = [
    BusData{Float64},
    GenData{Float64},
    BranchData{Float64},
    StorageData{Float64},
]

@testset "ExaPowerIO isbits tests" begin
    for row_type in ROW_TYPES
        @test isbitstype(row_type)
    end
end
